open Utils
module Ast = Language.Ast
module Const = Language.Const

type environment = {
  variables : Ast.expression Ast.TopDefMap.t;
  builtin_functions : (Ast.expression -> Ast.computation) Ast.TopDefMap.t;
}

let initial_environment =
  { variables = Ast.TopDefMap.empty; builtin_functions = Ast.TopDefMap.empty }

exception PatternMismatch

type computation_redex = Match | ApplyFun | DoReturn

type computation_reduction =
  | DoCtx of computation_reduction
  | ComputationRedex of computation_redex

let rec eval_tuple env = function
  | Ast.Tuple exprs -> exprs
  | Ast.Annotated (expr, _) -> eval_tuple env expr
  | Ast.TopDef x -> eval_tuple env (Ast.TopDefMap.find x env.variables)
  | expr ->
      Error.runtime "Tuple expected but got %t" (Ast.print_expression expr)

let rec eval_variant env = function
  | Ast.Variant (lbl, expr) -> (lbl, expr)
  | Ast.Annotated (expr, _) -> eval_variant env expr
  | Ast.TopDef x -> eval_variant env (Ast.TopDefMap.find x env.variables)
  | expr ->
      Error.runtime "Variant expected but got %t" (Ast.print_expression expr)

let rec eval_const env = function
  | Ast.Const c -> c
  | Ast.Annotated (expr, _) -> eval_const env expr
  | Ast.TopDef x -> eval_const env (Ast.TopDefMap.find x env.variables)
  | expr ->
      Error.runtime "Const expected but got %t" (Ast.print_expression expr)

let rec match_pattern_with_expression env pat expr =
  match pat with
  | Ast.PVar -> [ expr ]
  | Ast.PAnnotated (pat, _) -> match_pattern_with_expression env pat expr
  | Ast.PAs pat -> match_pattern_with_expression env pat expr @ [ expr ]
  | Ast.PTuple pats ->
      let exprs = eval_tuple env expr in
      List.map2 (match_pattern_with_expression env) pats exprs |> List.concat
  | Ast.PVariant (label, pat) -> (
      match (pat, eval_variant env expr) with
      | None, (label', None) when label = label' -> []
      | Some pat, (label', Some expr) when label = label' ->
          match_pattern_with_expression env pat expr
      | _, _ -> raise PatternMismatch)
  | Ast.PConst c when Const.equal c (eval_const env expr) -> []
  | Ast.PNonbinding -> []
  | _ -> raise PatternMismatch

let perform_abstraction env abstraction expr =
  let pat, binder = abstraction in
  let values = match_pattern_with_expression env pat expr |> Array.of_list in
  Bindlib.msubst binder values

let rec eval_function env = function
  | Ast.Lambda abstraction -> fun arg -> perform_abstraction env abstraction arg
  | Ast.RecLambda binder as expr ->
      fun arg ->
        let abstraction = Bindlib.subst binder expr in
        perform_abstraction env abstraction arg
  | Ast.Annotated (expr, _) -> eval_function env expr
  | Ast.TopDef x -> (
      match Ast.TopDefMap.find_opt x env.builtin_functions with
      | Some f -> f
      | None ->
          let expr = Ast.TopDefMap.find x env.variables in
          eval_function env expr)
  | expr ->
      Error.runtime "Function expected but got %t" (Ast.print_expression expr)

let step_in_context step env redCtx ctx term =
  let terms' = step env term in
  List.map (fun (red, term') -> (redCtx red, fun () -> ctx (term' ()))) terms'

let rec step_computation env = function
  | Ast.Return _ -> []
  | Ast.Match (expr, cases) ->
      let rec find_case = function
        | abstraction :: cases -> (
            match perform_abstraction env abstraction expr with
            | comp -> [ (ComputationRedex Match, fun () -> comp) ]
            | exception PatternMismatch -> find_case cases)
        | [] -> []
      in
      find_case cases
  | Ast.Apply (expr1, expr2) ->
      let f = eval_function env expr1 in
      [ (ComputationRedex ApplyFun, fun () -> f expr2) ]
  | Ast.Do (comp1, abstraction) -> (
      let comps1' =
        step_in_context step_computation env
          (fun red -> DoCtx red)
          (fun comp1' -> Ast.Do (comp1', abstraction))
          comp1
      in
      match comp1 with
      | Ast.Return expr ->
          ( ComputationRedex DoReturn,
            fun () -> perform_abstraction env abstraction expr )
          :: comps1'
      | _ -> comps1')

type load_state = {
  environment : environment;
  computations : Ast.computation list;
}

let initial_load_state =
  { environment = initial_environment; computations = [] }

let load_primitive load_state x prim =
  {
    load_state with
    environment =
      {
        load_state.environment with
        builtin_functions =
          Ast.TopDefMap.add x
            (Primitives.primitive_function prim)
            load_state.environment.builtin_functions;
      };
  }

let load_ty_def load_state _ = load_state

let load_top_let load_state x expr =
  {
    load_state with
    environment =
      {
        load_state.environment with
        variables = Ast.TopDefMap.add x expr load_state.environment.variables;
      };
  }

let load_top_do load_state comp =
  { load_state with computations = load_state.computations @ [ comp ] }

type run_state = load_state
type step_label = ComputationReduction of computation_reduction | Return
type step = { label : step_label; next_state : unit -> run_state }

let run load_state = load_state

let steps = function
  | { computations = []; _ } -> []
  | { computations = Ast.Return _ :: comps; environment } ->
      [
        {
          label = Return;
          next_state = (fun () -> { computations = comps; environment });
        };
      ]
  | { computations = comp :: comps; environment } ->
      List.map
        (fun (red, comp') ->
          {
            label = ComputationReduction red;
            next_state =
              (fun () -> { computations = comp' () :: comps; environment });
          })
        (step_computation environment comp)
